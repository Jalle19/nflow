buildscript {
  ext {
    springBootVersion = '1.5.4.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

plugins {
  id 'de.undercouch.download' version '3.1.1'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  nflowExplorer
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-jdbc')
  compile('org.springframework.boot:spring-boot-starter-jersey')
  compile('org.springframework.boot:spring-boot-starter-web')
  compile("io.nflow:nflow-rest-api:4.2.0")
  runtime('com.h2database:h2')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  nflowExplorer group: 'com.nitorcreations', name: 'nflow-explorer', version: '1.2.5', ext: 'tar.gz'
}

task resolveNflowExplorer(type: Copy) {
  destinationDir = file("$buildDir/resources/main/static/explorer")
  from { tarTree(resources.gzip(configurations.nflowExplorer.singleFile)) }
}

processResources.dependsOn resolveNflowExplorer
